#!/usr/bin/env zsh


dnscrypt-proxy-conf() {
	rconf=""

	[[ -r $H/run/leases ]] && {
		# load dhcp leases in redis
		for i in ${(f)"$(cat $H/run/leases)"}; do
			rconf+="SET dns-lease-${i[(w)4]} ${i[(w)3]}\n"
			rconf+="SET dns-lease-${i[(w)4]}.dowse.it ${i[(w)3]}\n"

			# and the hosts reverses
			rconf+="SET dns-reverse-${i[(w)3]}. ${i[(w)4]}\n"

		done
	}

	[[ -r /etc/dowse/network ]] && {
		for i in ${(f)"$(cat /etc/dowse/network)"}; do

			print "$i" | grep '^#' > /dev/null
			[[ $? = 0 ]] && continue

			# first word may be a mac address
			print "${i[(w)1]}" | grep '^..:..:..:..:..:..' > /dev/null
			if [[ $? = 0 ]]; then
				mac="${i[(w)1]}"
				hostname="${i[(w)2]}"
				ip4="${i[(w)3]}"
			else
				mac=""
				hostname="${i[(w)1]}"
				ip4="${i[(w)2]}"
			fi
			rconf+="SET dns-lease-${hostname} ${ip4}\n"
			rconf+="SET dns-lease-${hostname}.dowse.it ${ip4}\n"
			func "+/etc/network: $mac $hostname $ip4"
		done
	}

	# load the local hosts known
	rconf+="SET dns-lease-${hostname}          ${address}\n"
	rconf+="SET dns-lease-${hostname}.dowse.it ${address}\n"
	rconf+="SET dns-lease-www.dowse.it         ${address}\n"
	rconf+="SET dns-lease-dowse.it             ${address}\n"
	rconf+="SET dns-lease-gateway    ${wan}\n"
	# load the local hosts reverses
	rconf+="SET dns-reverse-${address}. ${hostname}.dowse.it\n"
	rconf+="SET dns-reverse-${wan}. gateway\n"
	print $rconf | redis dynamic
}

dnscrypt-proxy-exec() {

	args=""
	[[ $* =~ cache ]] && args+=",cache"
	[[ $* =~ debug ]] && args+=",debug"
	[[ $* =~ offline ]] && args+=",offline"


	func "Launching dnscrypt-proxy to listen on port 53"

    if [[ $DEBUG = 1 ]]; then
		DOWSE_DOMAINLIST=`pwd`/src/domain-list/data \
						DOWSE_LAN_ADDRESS_IP4=$address \
						hostname=$hostname \
						domain=$lan \
						launch dnscrypt-proxy -a $address:53 \
						-l $H/log/dnscrypt.log \
						-r "178.216.201.222:2053" \
						-k "25C4:E188:2915:4697:8F9C:2BBD:B6A7:AFA4:01ED:A051:0508:5D53:03E7:1928:C066:8F21" \
						-N "2.dnscrypt-cert.soltysiak.com" \
						-X $R/lib/dnscrypt-proxy/dnscrypt_dowse.so${args},debug \
						-m 7 -u $dowse_uid
		# log level informational (7 for debug)
		return $?

    else
		pid=$H/run/dnscrypt-proxy.pid
		DOWSE_DOMAINLIST=$R/domain-list/data \
						DOWSE_LAN_ADDRESS_IP4=$address \
						hostname=$hostname \
						domain=$lan \
						launch dnscrypt-proxy -a $address:53 \
						-p $pid -l $H/log/dnscrypt.log \
						-r "178.216.201.222:2053" \
						-k "25C4:E188:2915:4697:8F9C:2BBD:B6A7:AFA4:01ED:A051:0508:5D53:03E7:1928:C066:8F21" \
						-N "2.dnscrypt-cert.soltysiak.com" \
						-X $R/lib/dnscrypt-proxy/dnscrypt_dowse.so${args} \
						-d -m 4 -u $dowse_uid
		# log only warning conditions

    fi
    sleep 1
	# avoid race condition

    savepid dnscrypt-proxy $pid

    # TODO: offer a list of hosts from the csv list provided in dnscrypt source
}
