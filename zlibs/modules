#!/usr/bin/env zsh
#
# Copyright (C) 2012-2015 Dyne.org Foundation
#
# Dowse is designed, written and maintained by Denis Roio <jaromil@dyne.org>
#
# This source code is free software; you can redistribute it
# and/or modify it under the terms of the GNU Public License
# as published by the Free Software Foundation; either
# version 3 of the License, or (at your option) any later
# version.
#
# This source code is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied
# warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
# PURPOSE.  Please refer to the GNU Public License for more
# details.
#
# You should have received a copy of the GNU Public License
# along with this source code; if not, write to: Free
# Software Foundation, Inc., 675 Mass Ave, Cambridge, MA
# 02139, USA.

maps+=(dowse_mod)

dowse-module-load() {
    fn dowse-module-load
    func "Loading module: $1"

    # main module file is called dowse
    # skip module if not present
    core_script="$1/dowse"
    [[ -r "$core_script" ]] || {
        # check also relative path, reason we don't use ckreq
        core_script="$E/modules/enabled/$1/dowse"
        [[ -r "$core_script" ]] || return 1
    }

    # reset all variables that may be set by a  preceeding module
    command -v module-setup >/dev/null && unset -f module-setup
    command -v module-start >/dev/null && unset -f module-start
    command -v module-stop >/dev/null && unset -f module-stop

    dowse_mod=()
    #  dowse_mod_name
    #  dowse_mod_desc
    #  dowse_mod_tags
    #  dowse_mod_ports
    #  dowse_mod_daemons
    #  dowse_mod_authors
    #  dowse_mod_version

    # read all the module, instantiate functions
    source $core_script
    # save the id which is the directory name with underscores
    dowse_mod[id]=`basename "$1"`
    dowse_mod[id]="${dowse_mod[id]//-/_}"

    # check at least these 3 functions are declared
    command -v module-setup > /dev/null && \
        command -v module-start > /dev/null && \
        command -v module-stop > /dev/null || {
            warning "invalid module: $i"
            return 1 }

    func "loaded module: $1"
    # TODO check also header variables

    return 0
}

dowse-module-setup() {
    fn dowse-module-setup

    notice "Setup module: $dowse_mod[name] (${dowse_mod[id]})"
    act "$dowse_mod[desc]"

    # for dep in $dowse_mod_daemons; do

    #     func "automatically call ${dep}-extract from the daemons config directive"
    #     ${dep}-extract
    #     [[ $? = 0 ]] || {
    #         error "failed importing $dep"
    #         error "$dowse_mod_name depends on this daemon, cannot proceed loading"
    #         return 1 }
    # done

    # TODO: check if daemon is present in src/path.sh

    func "all daemons found: ${dowse_mod[daemons]}"
    func "ports opened: $dowse_mod[ports]"
    func "author: ${dowse_mod[authors]} version: $dowse_mod[version]"

    module-setup || {
        warning "cannot setup module: $dowse_mod_name"
        return 1 }
}

dowse-module-start() {
    fn dowse-module-start

    modname=${dowse_mod[id]}

    [[ "$modname" = "" ]] && {
        error "Module has no id: ${(k)dowse_mod}"
        return 1
    }

    notice "Starting module: $dowse_mod[name]"

    if module-start; then

        act "$dowse_mod[name] started succesfully"

        # add service listing to redis database
        redis_command="LPUSH mod-index \"$modname\"\n"
        # iterate through all fields (set in src/database.sh)
        for c in {2..${#mod}}; do
            local value=${dowse_mod[${mod[$c]}]}
            [[ "$value" = "" ]] && continue
            local key=${mod[$c]}
            redis_command+="SET mod-${modname}-${key} \"${value}\"\n"
        done
        # add service listing on redis
        func "$redis_command"
        # -n sets the database number (set in src/database.sh)
        print - $redis_command | redis runtime > /dev/null

    else
        warning "cannot start module $modname"
        return 1
    fi
}

dowse-module-list() {
    if [[ -r $H/run/redis.pid ]]; then
        print "LRANGE mod-index 0 -1" | redis runtime
    else
        for i in `find $E/modules/enabled -maxdepth 1 -type d | grep -v 'enabled$'`; do
            basename $i
        done
    fi
}

dowse-module-stop() {
    fn dowse-module-stop

    notice "Stopping module: $dowse_mod[name]"

    if module-stop; then
        act "$dowse_mod[name] stopped succesfully"
    else
        warning "problems stopping module: $dowse_mod[name]"
    fi
}
