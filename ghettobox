#!/usr/bin/env zsh
#
# Copyright (C) 2011-2013 Dyne.org Foundation
#
# Ghettobox is designed, written and maintained by Jaromil
#
# This source code is free software; you can redistribute it
# and/or modify it under the terms of the GNU Public License
# as published by the Free Software Foundation; either
# version 3 of the License, or (at your option) any later
# version.
#
# This source code is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied
# warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
# PURPOSE.  Please refer to the GNU Public License for more
# details.
#
# You should have received a copy of the GNU Public License
# along with this source code; if not, write to: Free
# Software Foundation, Inc., 675 Mass Ave, Cambridge, MA
# 02139, USA.


# {{{ GLOBALS

VERSION=0.1
DATE="Jul/2013"

QUIET=0
DEBUG=1

SCRIPT=$0

DIR=${GHETTOBOX:-`pwd`}

# standard output message routines
autoload colors; colors
# it's always useful to wrap them, in case we change behaviour later
notice() { if [[ $QUIET == 0 ]]; then print "$fg_bold[green][*]$fg_no_bold[default] $1" >&2; fi }
error()  { if [[ $QUIET == 0 ]]; then print "$fg[red][!]$fg[default] $1" >&2; fi }
func()   { if [[ $DEBUG == 1 ]]; then print "$fg[blue][D]$fg[default] $1" >&2; fi }
act()    {
    if [[ $QUIET == 0 ]]; then
	if [ "$1" = "-n" ]; then
	    print -n "$fg_bold[white] . $fg_no_bold[default] $2" >&2;
	else
	    print "$fg_bold[white] . $fg_no_bold[default] $1" >&2;
	fi
    fi
}


# honor quiet and debug flags as early as possible
if [[ ${@} == *-q* ]]; then QUIET=1; fi
if [[ ${@} == *-D* ]]; then DEBUG=1; fi


# pid files for our daemons
pid_squid=$DIR/run/squid.pid
pid_dnsmasq=$DIR/run/dnsmasq.pid
pid_privoxy=$DIR/run/privoxy.pid

# }}}


# {{{ CHECKS

{ test -r conf } || {
    error "The ghettobox script must be run inside its source directory"
    return 1 }

{ test -r conf/settings } || {
    error "Ghettobox configuration is missing, create conf/settings"
    return 1 }

{ test -r conf/network } || {
    error "No network is configured, create conf/network"
    return 1 }

notice "Ghettobox $VERSION ($DATE)"
func "loading configuration from $DIR/conf/settings"
source conf/settings

# setup dirs
mkdir -p log
chmod go-rwx log
chown $ghettouid:$ghettogid log

mkdir -p run
chmod go-rwx run
chown $ghettouid:$ghettogid run

# create the cache dir in RAM
mkdir -p /dev/shm/ghettobox
chown $ghettouid:$ghettogid /dev/shm/ghettobox
chmod go-rwx /dev/shm/ghettobox

# }}}



# {{{ SETUP
ghetto_setup() {
    act "Configuring our ghetto and connecting it to the Internet..."

    func "reading configuration in conf/settings"

    func "generating dnsmasq.conf"
    cat <<EOF > $DIR/run/dnsmasq.conf
address=/$ghettoname/$ghettodns
address=/.i2p/$ghettodns
address=/.onion/$ghettodns
bogus-priv
cache-size=1500
conf-dir=/etc/dnsmasq.d
dhcp-range=$ghettoguests
addn-hosts=$DIR/run/hosts
dhcp-leasefile=$DIR/run/leases
domain-needed
domain=$ghettodomain
expand-hosts
interface=$ghettoiface
listen-address=$ghettodns,127.0.0.1
local=/$ghettodomain/
user=$ghettouid
group=$ghettogid
EOF

    func "generating privoxy.conf"
    cat <<EOF > $DIR/run/privoxy.conf
user-manual /usr/share/doc/privoxy/user-manual
confdir /etc/privoxy
logdir $DIR/log/privoxy
listen-address  0.0.0.0:8118
toggle  1
enable-remote-toggle  0
enable-remote-http-toggle  0
enable-edit-actions 1
enforce-blocks 0
buffer-limit 64000

forwarded-connect-retries  0
accept-intercepted-requests 1
allow-cgi-request-crunching 0
split-large-forms 0
keep-alive-timeout 5
socket-timeout 300
handle-as-empty-doc-returns-ok 1


# pass through tor for urls.onion
forward-socks4a .onion $ghettodns:9050 .

# TODO: tor might use polipo (web cache)
# forward / $ghettodns:8123
# forward / 127.0.0.1:8123

# pass through i2p for urls.i2p
forward .i2p $ghettodns:4444
forward .i2p 127.0.0.1:4444

# direct access
forward $ghettoname .

filterfile default.filter
actionsfile match-all.action # Actions that are applied to all sites and maybe overruled later on.
actionsfile default.action   # Main actions file
actionsfile user.action      # User customizations
EOF

func "generating squid.conf"

cat <<EOF > $DIR/run/squid.conf
pid_filename $pid_squid
cache_store_log none
cache_log $DIR/log/squid_cache.log
access_log $DIR/log/squid_access.log squid

# avoid having a physical cache directory
#cache_dir aufs /dev/shm/ghettobox 300
cache_dir aufs /dev/shm/ghettobox 300 16 256
maximum_object_size 10 MB
minimum_object_size 512 KB

acl all src all
acl manager proto cache_object
acl localhost src 127.0.0.1/32
acl to_localhost dst 127.0.0.0/8 0.0.0.0/32

acl localnet src $ghettonet

acl SSL_ports port 443		# https
acl Safe_ports port 80		# http
acl Safe_ports port 443		# https
acl purge method PURGE
acl CONNECT method CONNECT

http_access allow manager localhost
http_access deny manager
http_access allow purge localhost
http_access deny purge
http_access deny !Safe_ports
http_access deny CONNECT !SSL_ports

http_access allow localnet
http_access allow localhost

http_access deny all

icp_access allow localnet
icp_access deny all

http_port 3128 transparent

hierarchy_stoplist cgi-bin ?

refresh_pattern ^ftp:		1440	20%	10080
refresh_pattern ^gopher:	1440	0%	1440
refresh_pattern -i (/cgi-bin/|\?) 0	0%	0
refresh_pattern (Release|Packages(.gz)*)$	0	20%	2880
refresh_pattern .		0	20%	4320

acl shoutcast rep_header X-HTTP09-First-Line ^ICY.[0-9]
upgrade_http0.9 deny shoutcast

acl apache rep_header Server ^Apache
broken_vary_encoding allow apache

extension_methods REPORT MERGE MKACTIVITY CHECKOUT

cache_mgr Ghettobox

hosts_file $DIR/run/hosts

coredump_dir $DIR/log

cache_peer localhost parent 8118 0 default no-query no-digest no-netdb-exchange
never_direct allow all

header_access From deny all

# the settings below are restrictive:
# they grant more privacy but break many websites!
# header_access Link deny all
# header_access Server deny all
# header_access Referer deny all
# header_access User-Agent deny all
# header_access WWW-Authenticate deny all

EOF

func "Fixing entries for known ghetto inhabitants"
known=`cat conf/network | grep -v '^#'`
rm -f $DIR/run/dnsmasq.network
# this is basically a dnsmasq host configuration file
print "dhcp-option=option:router,$ghettobox" > $DIR/run/dnsmasq.network

# this is our generated hosts file
rm -f $DIR/run/hosts; touch $DIR/run/hosts

for i in ${(f)known}; do
    func "$i"
    # gather configuration into variables, line by line
    mac=${i[(w)1]}
    host=${i[(w)2]}
    ip=${i[(w)3]}

    # add a line to the dnsmasq host list
    print "dhcp-host=$mac, $host, $ip" >> $DIR/run/dnsmasq.network

    { test "$host" = "ignore" } || {
    # add a line to the hosts list
	print "$ip $host" >> $DIR/run/hosts }
done

notice "Setup completed in $DIR"
return 0
}
# }}}

# {{{ START/STOP FORMULAS


dnsmasq_stop() {
    { test -r $pid_dnsmasq } && {
	act "Stopping dnsmasq (`cat $pid_dnsmasq`)"
	kill `cat $pid_dnsmasq`
	rm -f $pid_dnsmasq
    }
}
dnsmasq_start() {
    act "Preparing to launch dnsmasq..."

    # if running, stop to restart
    dnsmasq_stop

    func "dnsmasq --pid-file $DIR/log/dnsmasq.pid -C $DIR/dnsmasq.conf"
    dnsmasq --pid-file=$pid_dnsmasq -C $DIR/run/dnsmasq.conf
}

squid_stop() {
    { test -r $pid_squid } && {
	act "Stopping squid (`cat $pid_squid`)"
	squid -f $DIR/run/squid.conf -k shutdown
#	kill `cat $pid_squid`
#	rm -f $pid_squid
    }
}
squid_start() {
    act "Preparing to launch Squid..."

    # if running, reconfigure
    { test -r $pid_squid } && {
	squid -f $DIR/run/squid.conf -k reconfigure
	return 0 }

    func "setuidgid $ghettouid squid -f $DIR/run/squid.conf"
    # populate the volatile cache
    setuidgid $ghettouid squid -z -f $DIR/run/squid.conf
    # launch the squid
    setuidgid $ghettouid squid -f $DIR/run/squid.conf
}

privoxy_stop() {
    { test -r $pid_privoxy } && {
	act "Stopping privoxy (`cat $pid_privoxy`)"
	kill `cat $pid_privoxy`
	rm -f $pid_privoxy
    }
}
privoxy_start() {
    act "Preparing to launch privoxy..."

    # if running, stop to restart
    privoxy_stop

    func "setuidgid $ghettouid privoxy --pidfile $pid_privoxy $DIR/privoxy.conf"
    privoxy --user $ghettouid --pidfile $pid_privoxy $DIR/run/privoxy.conf
}
# }}}


# {{{ START
ghetto_start() {
    act "Checking requirements to run ghettobox..."

    func "required programs"
    for req in dnsmasq iptables privoxy squid; do
	command -v $req >/dev/null
	{ test $? != 0 } && {
	    error "Cannot find $req. You require more BLINGBLING man, please install it."
	    return 1
	}
    done

    func "root access"
    { test "$UID" = "0" } || {
	error "Ghettobox needs root access to run."
	return 1 }


    notice "Setting up the ghetto network..."

    PGL=`pidof pgld`
    { test "$PGL" = "" } || {
	act "PeerGuardian found running, will restart it accordingly"
	pglcmd stop }


    act "Creating a virtual bridge"
    ifconfig | grep '^br0' > /dev/null
    if [ $? != 0 ]; then brctl addbr br0; fi
    ifconfig br0 $ghettobox netmask 255.255.255.0 up

    func "enable masquerading over bridge"
    sysctl net.netfilter.nf_conntrack_acct=1

    func "enable ip forwarding"
    print 1 > /proc/sys/net/ipv4/ip_forward

    func "set up ip masquerading"
    iptables --flush
    iptables --table nat --flush
    iptables --delete-chain
    iptables --table nat --delete-chain

    func "setup route towards wired network"
    iptables --table nat --append POSTROUTING --out-interface eth0 -j SNAT --to $ghettodns
    iptables --append FORWARD --in-interface $ghettoiface -s $ghettonet -j ACCEPT

    func "setup transparent proxy to squid"
    iptables -t nat -A PREROUTING -i $ghettoiface -s $ghettonet -p tcp --dport 80 \
	-j REDIRECT --to-port 3128

    func "defend the ghetto from ipv6"
    ip6tables -F
    ip6tables -P INPUT DROP
    ip6tables -P FORWARD DROP
    ip6tables -P OUTPUT DROP

    # start the squid daemon
    squid_start

    # start the privoxy daemon
    privoxy_start

    act "Pinning down MAC addresses in ARP entries..."
    known=`cat conf/network | grep -v '^#'`
    for i in ${(f)known}; do
	func "$i"
    # gather configuration into variables, line by line
	mac=${i[(w)1]}
	host=${i[(w)2]}
	ip=${i[(w)3]}

	{ test "$host" = "ignore" } && { continue }

	arp -s $ip $mac

    done

    # start the dnsmasq daemon
    dnsmasq_start

    # if PeerGuardian was running, start it again
    { test "$PGL" = "" } || { pglcmd start }

}
# }}}

# {{{ STOP
ghetto_stop() {
    act "Stopping all Ghettobox services."
    { test -r $DIR/log/squid.pid } && { kill `cat $DIR/log/squid.pid` }
    { test -r $DIR/log/privoxy.pid } && { kill `cat $DIR/log/privoxy.pid` }
    { test -r $DIR/log/dnsmasq.pid } && { kill `cat $DIR/log/dnsmasq.pid` }

}
# }}}

# {{{ MAIN

# we use a very simple argument parser
case "$1" in
    restart|start) ghetto_setup; ghetto_start ;;
    release) rm $DIR/run/leases; ghetto_setup; ghetto_start ;;
    stop) ghetto_stop ;;
    *) echo "ghettobox: command not found: $1" ;;
esac

# }}}
