#!/usr/bin/env zsh
#
# Copyright (C) 2012-2016 Dyne.org Foundation
#
# Designed, written and maintained by Denis Roio <jaromil@dyne.org>
#
# This source code is free software; you can redistribute it
# and/or modify it under the terms of the GNU Public License
# as published by the Free Software Foundation; either
# version 3 of the License, or (at your option) any later
# version.
#
# This source code is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied
# warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
# PURPOSE.  Please refer to the GNU Public License for more
# details.
#
# You should have received a copy of the GNU Public License
# along with this source code; if not, write to: Free
# Software Foundation, Inc., 675 Mass Ave, Cambridge, MA
# 02139, USA.


# {{{ GLOBALS

dowse_version=0.9
dowse_release_date="Mar/2016"

zkv=1
restful=1
helpers=1

R=${DOWSE_PATH:-`pwd`}

# load development libraries if there
if [[ -r $R/zlibs/zuper-dev/zuper ]]; then
    func "Zuper loaded from development symlink"
    source $R/zlibs/zuper-dev/zuper
else source $R/zlibs/zuper; fi

# For gettext
TEXTDOMAIN=dowse

# path and debugging
vars+=(R address_loaded debug quiet script fun dowserc)
vars+=(last_notice last_act last_func)
vars+=(root)
arrs+=(mods)

# export DOWSE to env with path to the installation
# else start it from inside the dir with source dowse
DEBUG=${DEBUG:-0}
QUIET=${QUIET:-0}
LOG=${LOG:-""}


# configuration
vars+=(interface hostname dns lan firewall)
# uid/gid
vars+=(dowse_uid dowse_gid)
# network
vars+=(wan address dowse_net netmask dowse_guests)


# globals filled by lease parser
arrs+=(host mac ip)

# globals used when loading modules
# vars+=(dowse_mod_name dowse_mod_desc dowse_mod_version)
# arrs+=(dowse_mod_type dowse_mod_ports dowse_mod_deps)
# arrs+=(dowse_mod_authors dowse_mod_tags)
maps+=(dowse_mod)

# consul's configuration map
# maps+=(consul_conf consul_watch)

# redis database indexes
maps+=(db)

# dataset indexes
arrs+=(mod)

# daemons exec paths (OS dependent)
maps+=(execmap)
vars+=(execpath)

# default daemons conf and pids
vars+=(pid conf)

zmodload zsh/system
zmodload zsh/mapfile
zmodload zsh/regex
zmodload zsh/sched
zmodload zsh/system
zmodload zsh/net/tcp

source $R/zlibs/modules
source $R/zlibs/processes
source $R/zlibs/iptables
source $R/zlibs/ebtables
source $R/zlibs/confstore
source $R/zlibs/apt
source $R/zlibs/redis

source $R/daemons/consul
source $R/daemons/dnsmasq
source $R/daemons/squid3
source $R/daemons/privoxy
source $R/daemons/polipo
source $R/daemons/dnscap
source $R/daemons/pgl
source $R/daemons/tor
source $R/daemons/redis
source $R/daemons/dnscrypt-proxy

# conclude initialization
if [[ -r $R/zlibs/zuper-dev/zuper.init ]]; then
    source $R/zlibs/zuper-dev/zuper.init
else source $R/zlibs/zuper.init; fi


func "load dataset indexes"
db=()
execmap=()
zkv.load $R/src/database.zkv
zkv.load $R/src/execmap.zkv

# module fields
mod=(
    name
    desc
    tags
    ports
    webgui
    daemons
    authors
    version
)


if [[ $dowse_loaded = 1 ]]; then
    act "Dowse CLI reloaded ::1::" $dowse_version
    RPROMPT=Dowse
else
    notice "Dowse CLI ::1:: - local area network rabdomancy" $dowse_version
    dowse_loaded=1
    RPROMPT=Dowse
fi

cat <<EOF

 Copyright (C) 2012-2016 Dyne.org Foundation, License GNU GPL v3+
 This is free software: you are free to change and redistribute it
 For more informations see http://www.dyne.org/software/dowse

EOF

# fn dowse-check one arg
arg=${1:-conf/settings}
# conf is global
dowserc="$R/$arg"

freq=($R/conf $dowserc)

func "reading configurations in $R/conf"
ckreq && conf-load

act "checking requirements to run dowse..."

[[ "$address" = "" ]] && {
    error "Invalid config file"
    zdump
    return 1
}

mkdir -p $R/log
mkdir -p $R/run

root=1
[[ "$UID" = "0" ]] || unset root
if [[ "$root" = "" ]]; then
    warn "no root privileges, limited functionality"
else
    act "root privileges granted"
fi


dowse_uid=`id -un`
dowse_gid=`id -gn`

_verbose "Dowse command: ::1 commands::" "$*"
_verbose "Caller: uid[::1 uid::], gid[::2 gid::], tty[::3 tty::]." \
         $dowse_uid $dowse_gid $TTY

act "daemons running as user and group $dowse_uid:$dowse_gid"
# [[ -z $root ]] || {
#     # setup dirs
#     chmod go-rwx $R/log
#     chown -R $dowse_uid:$dowse_gid $R/log

#     chmod go-rwx $R/run
#     chown -R $dowse_uid:$dowse_gid $R/run
# }

# func "::1 dir:: directory writable by ::2 uid::" run $dowse_uid
# if touch $R/run/test; then rm -f $R/run/test; else return 1; fi

# func "::1 dir:: directory writable by ::2 uid::" log $dowse_uid
# if touch $R/log/test; then rm -f $R/log/test; else return 1; fi


act "all requirements in place"

fn dowse-running
# }}}



# {{{ MAIN

# Here is most of the hard-coded logic of dowse when starting up
# eventually some of this need to interact, acknowledge or even
# be moved into modules, depending from future developments.
# Meanwhile, special care is taken to keep this readable and
# transparent (avoid wrapping when possible), documenting choices
# with references.
dowse-start() {
    fn dowse-start
    # freq=($R/conf/settings $R/conf/network)
    # ckreq || return $?

    [[ -r $R/run/redis-server.pid ]] && {
        warn "Dowse is already running"
        return 0
    }
    notice "Starting Dowse"

    # start the redis daemon (core k/v service)
    redis-conf > $R/run/redis-server.conf
    cat <<EOF >> $R/run/redis-server.conf
pidfile $R/run/redis-server.pid
logfile $R/log/redis-server.log
port 6379
EOF
    start redis-server

    [[ -z $root ]] || {


        PGL=`pidof pgld`
        [[ "$PGL" = "" ]] || {
            act "PeerGuardian found running, will restart it accordingly"
            pgl-stop }

        # for the firewall stuff see: zlibs/iptables and zlibs/ebtables
        [[ "$firewall" = "yes" ]] && {

            ebtables-stop
            ebtables-start

            iptables-stop
            iptables-start

            iptables-base-protection

            loopback-on
            if loopback-only; then
                act "dry run on localhost only"
            else
                loopback-isolate

                act "set up $interface interface with ip $address"
                ifconfig $interface $address netmask $netmask up

                act "set $wan as default gateway"
                route add default gw $wan

                act "setup snat trasversal"
                iptables-snat-on
            fi
        }

        # generate the dnsmasq configuration
        dnsmasq-conf > $R/run/dnsmasq.conf

        # tunnel dnsmasq across dnscrypt-proxy
        cat <<EOF >> $R/run/dnsmasq.conf
server=127.0.0.1#53443
EOF

        # make sure the dowse host uses his own dns
        mv /etc/resolv.conf /etc/resolv.conf.dowse-backup
        cat <<EOF > /etc/resolv.conf
nameserver 127.0.0.1
# domain $lan
# search $lan
EOF
        # launch the dnsmasq daemon
        start dnsmasq

    }

    # start the dns encrypted tunneling
    start dnscrypt

    # these are root programs that need redis to be running
    [[ -z $root ]] || start dnscap


    mods=`find -L $R/modules/enabled -maxdepth 1 -type d | grep -v 'enabled$'`
    for m in "${(f)mods}"; do
        dowse-module-load $m || continue
        dowse-module-setup || continue
    done

    for m in "${(f)mods}"; do
        dowse-module-load $m || continue
        dowse-module-start || continue
    done

    [[ "$firewall" = "yes" ]] && {
        [[ -z $root ]] || {
            # start peerguardian (must be at end, after any other iptables setting)
            pgl-conf > $R/run/pgl/etc/pgl/pglcmd.conf
            start pgl
        }
    }

    notice "Dowse succesfully started" # , web interface up on:"
    # act "http://${address}:8500/"

}

dowse-stop() {
    fn dowse-stop

    for m in `dowse-module-list`; do

        dowse-module-load $m || continue
        dowse-module-stop

    done

    # stop the dns crypto tunnel
    stop dnscrypt

    [[ -z $root ]] || {
        stop dnscap
        stop dnsmasq

        pgl-kill
        iptables-snat-off
        iptables-stop
        ebtables-stop
        # restore backup if present
        [[ -r /etc/resolv.conf.dowse-backup ]] &&  {
            mv /etc/resolv.conf.dowse-backup /etc/resolv.conf
        }
    }

    stop redis

    # consul-stop

    notice "Dowse has stopped running."

}

dowse-reload() {
    fn dowse-reload
    freq=($R/modules/enabled $R/run/redis-server.pid)
    ckreq || return 1

    mods=`find -L $R/modules/enabled -maxdepth 1 -type d | grep -v 'enabled$'`
    func "modules enabled: $mods"
    for m in "${(f)mods}"; do
        dowse-module-load $m || continue
        dowse-module-stop
    done

    for m in "${(f)mods}"; do
        dowse-module-load $m || continue
        dowse-module-setup
    done

    for m in "${(f)mods}"; do
        dowse-module-load $m || continue
        dowse-module-start
    done

}

dowse-parse-leases() {
    awk '
BEGIN { c=0 }
{
  mac[c]=$2
  ip[c]=$3
  if($4=="*") host[c]="anonymous"; else host[c]=$4
  c++
}
END {
  printf "mac=("; for(i=0;i<c;i++) { printf "%s ", mac[i] }; printf ")\n"
  printf "ip=("; for(i=0;i<c;i++) { printf "%s ", ip[i] }; printf ")\n"
  printf "host=("; for(i=0;i<c;i++) { printf "%s ", host[i] }; printf ")\n"
}' $R/run/leases
}

dowse-dump() {
    fn dowse-dump

    freq=($R/run/redis.pid)
    ckreq || return $?

    notice "Dumping variables in Redis"
    act "Runtime:"
    print - "KEYS *" | redis runtime | sort
    act "Storage:"
    print - "KEYS *" | redis storage | sort
    act "Dynamic:"
    print - "KEYS *" | redis dynamic | sort
}

dowse-status() {
    fn dowse-status

    req=(address)
    freq=($R/run/leases)
    ckreq || return $?

    typeset -a mac ip host ping known
    notice "Reporting network status"
    eval `dowse-parse-leases`
    known=()
    ping=()

    for i in {0..${#mac}}; do
        [[ $mac[$i] = "" ]] && { continue }

        known[$i]="unknown"
        grep "${mac[$i]}" $R/conf/network > /dev/null
        [[ $? = 0 ]] && known[$i]="known"

        ping[$i]="offline"
        nmap -sn -n -oG - $ip[$i] | grep -i 'status: up' > /dev/null
        [[ $? = 0 ]] && ping[$i]="online"

        if [[ "$known[$i]" == "known" ]]; then

            yes "$ping[$i]\t $mac[$i]\t $ip[$i]\t $host[$i]"

        else

            no "$ping[$i]\t $mac[$i]\t $ip[$i]\t $host[$i]"

        fi
    done
}

dowse-scan() {
    fn dowse-scan
    freq=($R/run/leases)
    ckreq || return $?

    notice "Scanning unknown network objects"
    eval `dowse-parse-leases`

    for s in {0..${#mac}}; do
        [[ $mac[$s] == "" ]] && { continue }
        grep "${mac[$s]}" $R/conf/network > /dev/null
        [[ $? == 1 ]] && { # unknown
            act "Running scan on ::1 ip:: (::2 host::)" ${ip[$s]} ${host[$s]}
            func "$s: nmap -A -F -sT -Pn -T5 \"${ip[$s]}\""
            nmap -A -F -sT -Pn -T5 -n "${ip[$s]}"
            print
        }
    done

}

# }}}
